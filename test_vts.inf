! ------------------------------------------------------------------------------
! Tests - Tests for the VTS game code.
!
! Created by Jessica McIntosh
! JessicaKMcIntosh AT gmail DOT com
!
! This work is licensed under the Creative Commons Attribution-ShareAlike 4.0
! International License. See the file LICENSE for details.
! ------------------------------------------------------------------------------

! ------------------------------------------------------------------------------
! Meta Data
! ------------------------------------------------------------------------------

Release 0;

! Replace the Main routine from the standard library.
Replace Main;

! ------------------------------------------------------------------------------
! Libraries
! ------------------------------------------------------------------------------

! Standard libraries.
Include "Parser";
Include "VerbLib";
Include "Grammar";

! Libraries used for testing.
Include "istring";
Include "infunit";
Include "infunit_ext";

! Game specific testing libraries.
Include "testpills";
Include "testtape";

! ------------------------------------------------------------------------------
! Test Objects
! ------------------------------------------------------------------------------

! For checking messages. See Extensions/istring.h for more information.
Array PrintedString->MAX_STR_LEN;
Array CheckString->MAX_STR_LEN;

! Fake player.
Object Fake_Player "Fake Player"
;

! ------------------------------------------------------------------------------
! Test Code
! ------------------------------------------------------------------------------

! Start capturing output to the array PrintedString.
[CaptureStart;
  @output_stream 3 PrintedString;
];

! Stop capturing output.
[CaptureStop;
  @output_stream -3;
];

! Run a routine and capture the output to the array PrintedString.
[CaptureOutput capture_routine;
  CaptureStart();
  capture_routine();
  CaptureStop();
];

! Prepare objects for each test.
! Resets state for each test so they all have the same starting point.
[PrepareObjects;
  ! Prepare the player.
  player = Fake_Player;
  deadflag = 0;

  ! Prepare action processing.
  action = nothing;
  inp1 = nothing;
  inp2 = nothing;
  noun = nothing;
  second = nothing;

  ! Prepare the scoring variables.
  score = 0;
  last_score = 0;
  places_score = 0;
  things_score = 0;

  ! Other random variables.
  lookmode = 2;
  turns = 0;

  ! Remove everything from the player object.
  while(child(player)) {
    remove(child(player));
  }
];

! ------------------------------------------------------------------------------
! Run Tests
! ------------------------------------------------------------------------------

! Make the standard library happy.
[Initialise;
];

! Main function for running the tests.
[Main;
  print "Running tests for VTS.^^";

  ! Run the Pills tests.
  PrepareObjects();
  TestPillRun();

  ! Run the Tape tests.
  PrepareObjects();
  TestTapeRun();

  reportCustom();
];