! ------------------------------------------------------------------------------
! Volunteer For Survival - A survival horror text adventure game.
!
! Created by Jessica McIntosh
! JessicaKMcIntosh AT gmail DOT com
!
! This work is licensed under the Creative Commons Attribution-ShareAlike 4.0
! International License. See the file LICENSE for details.
! ------------------------------------------------------------------------------

! ------------------------------------------------------------------------------
! Meta Data
! ------------------------------------------------------------------------------

Release 0;
Serial "230914";

! The player will die when moving around in the dark.
Replace DarkToDark;

! ------------------------------------------------------------------------------
! Constants
! ------------------------------------------------------------------------------

Constant Story "Volunteer For Survival";
Constant Headline
 "^A survival horror text adventure game.^
  By Jessica McIntosh <JessicaKMcIntosh AT gmail DOT com>^
  ^
  It's been five years since the aliens invaded earth. We got an early warning^
  of their approach so we were prepared. Shelters were built to house people^
  during the conflict. Unfortunately, due to hubris, the world governments^
  expected to defeat the aliens quickly. As a result the shelters were only^
  intended to last for a decade. Thanks to overcrowding, the supplies have^
  nearly run out. For the past year volunteers have been sent out to collect^
  supplies. You are only the latest.^
  ^
  Welcome to Vololtol city.^
  ^
  Can you survive the alien wasteland and make it back to the shelter?^^";
Constant OBJECT_SCORE 1;
Constant MAX_SCORE = 7;

! ------------------------------------------------------------------------------
! Libraries
! ------------------------------------------------------------------------------

! Standard libraries and extensions.
Include "parser";
Include "verblib";
Include "grammar";

! Game specific libraries.
Include "help";
Include "lamp";
Include "loot";
Include "pills";
Include "tape";
Include "use";
Include "xyzzy";

! The world.
Include "classes";
Include "city";
Include "notebook";
Include "subway";
Include "storm";
Include "starting";
Include "stuff";

! ------------------------------------------------------------------------------
! Items - Items that don't have any particular home.
! ------------------------------------------------------------------------------

Tape_Player_Class Tape_Player "tape player"
  with
    description
     "An old microcassette tape player.^
      To play a tape use the command: PLAY TAPE NAME",
;

Tape_Cassette_Class -> Tape_0 "tape #0"
  with
    name 'cassette' 'tape' '#0' "0",
    description "A microcassette tape containing a message from yourself.",
    inside_description
     "Hey there, uh, well, me. They tell me I will not remember anything once I take^
      this drug. So I need to tell you, or, myself, what we are supposed to be doing.^
      The drug is going to make me forget everything. I'll just do the short^
      version. A bad thing happened. We are living in shelters. The, things, out^
      there are psychic or something. I don't really understand this stuff, I'm just^
      a janitor. Unfortunately we need food and tech to survive. So us volunteers are^
      sent out into the world to collect what we need. Basically anything useful,^
      check the list in your notebook. They gave me what few supplies they can spare,^
      which frankly isn't much. Just a backpack, some food, a strange radio and a^
      fucking crowbar. Anyway, they are about to send me out so I'll end this. Oh,^
      one last thing, they said if I do a good job I'll get my own room, all to^
      myself. Bye.",
  after [;
    Play:
      ! Move the notebook page representing this tape into the notebook.
      Notebook.add(Notebook_Tape_0);
  ],
  has scored
;

Pills_Class Pill_Bottle "pill bottle"
;

! Notebook pages for each of the tapes that can be played.
Notebook_Page_Class Notebook_Tape_0 "Cassette tape #0"
with
    description "A microcassette tape containing a message from yourself.",
    name 'cassette' 'tape' '#0' "0",
    with_key Tape_0,
;

Notebook_Page_Class Notebook_Tape_1 "Cassette tape #1"
  with
    name 'cassette' 'tape' '#1' "1",
    description "A microcassette tape containing a message from another volunteer.",
    with_key Tape_1,
;

Notebook_Page_Class Notebook_Tape_2 "Cassette tape #2"
  with
    name 'cassette' 'tape' '#2' "2",
    description "A microcassette tape containing a message from another volunteer.",
    with_key Tape_2,
;

Notebook_Page_Class Notebook_XYZZY_Token "XYZZY Token"
  with
    name 'xyzzy' 'token',
    description "A token given to those that say the magic word.",
    inside_description
     "A token given to those that say the magic word.^
      Nothing happens when speaking the word.^
      But in honor of doing so you are given this lovely token.^
      ^
      It might even do something useful. (Probably not.)",
;

! ------------------------------------------------------------------------------
! Initialize the game
! ------------------------------------------------------------------------------

[ Initialise;
  location = Start_Room;

  ! If RANDOM_SEED is defined use it to seed the random number generator.
  ! This is purely for testing purposes.
  #Ifdef RANDOM_SEED;
    print "NOTE: Seeding the random number generator with ", RANDOM_SEED, ".^";
    random(RANDOM_SEED);
  #Endif;

  ! Setup the loot.
  SetupLoot();

  ! Give the player the microcassette player.
  move Tape_Player to player;

  ! Give the player the bottle of pills then start the Daemon.
  move Pill_Bottle to player;
  StartDaemon(Pill_Bottle);

  ! Make sure the starting notebook pages have numbers.
  Notebook.update();

! TODO: Delete this in the final version.
  print
   "This game is still in Beta and will have bugs.^
    Email JessicaKMcIntosh AT gmail DOT com to report issues.^
    Type HELP for in game help.^
    For more information please visit:^
    https://github.com/JessicaKMcIntosh/VolunteerForSurvival/wiki^";
];

! ------------------------------------------------------------------------------
! Subroutines
! ------------------------------------------------------------------------------

! Handle the player moving from one dark location to another. (ie. kill them)
[ DarkToDark;
  deadflag = 1;
  "You have been attacked by a vicious creature in the dark.^
   As you lay there bleeding out you reconsider not using the lantern.";
];

! Gives the player all the items they can collect.
[ AllSub
  item;
  ! Give key items.
  move Camping_Lantern to player;
  move Crowbar to player;
  move Notebook to player;
  move Pill_Bottle to player;
  move Radio to player;
  move Tape_Player to player;
  move XYZZY_Token to player;

  ! Give all of the notebook pages.
  objectloop (item ofclass Notebook_Page_Class) {
    if (item notin Notebook) {
      Notebook.add(item);
    }
  }

  ! Give all of the cassette tapes.
  objectloop (item ofclass Tape_Cassette_Class) {
    if (item notin player or Tape_Player) {
      move item to player;
    }
  }
];

! ------------------------------------------------------------------------------
! Grammar
! ------------------------------------------------------------------------------

! For debugging, give the player all items.
Verb 'all' * -> All;
